# This policy uses the HTTP import to call the TFC API to get a list of all
# modules in a specified module registry and determine their
# latest versions. It then uses the tfconfig/v2 import to inspect all non-root
# modules and validate that those sourced from the registry allow the latest
# versions based on their version constraints. Since Terraform will always use
# the most recent version allowed by a version constraint, this ensures that
# the most recent version of the module will be used.

# Note that this policy requires the Sentinel runtime 0.16.0 or higher since it
# uses the Sentinel [version](https://docs.hashicorp.com/sentinel/imports/version) import.

# Additionally, this policy uses Sentinel parameters

# The policy supports pagination to get unlimited number of modules from private
# registries. Since the public registry has a very large number of modules,
# the policy does not use pagination for it and limits the total number of
# modules to 100.

# This policy currently uses the /api/registry/v1/modules endpoint for private
# registries rather than the newer /organizations/:organization_name/registry-modules
# endpoint that can get both private and publicly curated modules. Note that
# publically curated modules are not available in TFE. The
# /organizations/:organization_name/registry-modules endpoint is available
# in TFE since version v202106-1. We expect to create a version of this policy
# that will use the new API endpoint but we will keep this policy so that
# customers on older versions of TFE can still use it.

##### Imports #####

# Import common-functions/tfconfig-functions/tfconfig-functions.sentinel
# with alias "config"
import "tfconfig-functions" as config

# Standard Imports
import "http"
import "strings"
import "json"
import "version"

##### Parameters #####
# A boolean indicating whether the public Terraform registry is being used
param public_registry default false
# The address of the Terraform Cloud or Terraform Enterprise server
param address default "app.terraform.io"
# The limit to use when retrieving modules from a registry
# This cannot be greater than 100
param limit default 100
# The name of the Terraform Cloud or Terraform Enterprise organization
param organization
# A valid Terraform Cloud or Terraform Enterprise API token
param token

##### Functions #####

# Retrieve modules from a module registry and give their paths and latest versions
retrieve_latest_module_versions = func() {

  modules = {}

  # Call the TFC Modules API and extract the response
  # Treat public registry and private registry differently
  if public_registry {
    modules = get_public_registry_modules()
  } else {
    # private registry
    # Pass the empty modules map and offset=0
    modules = get_private_registry_modules(modules, 0)
  }

  # modules is indexed by <name>/<provider> and contains most recent version
  return modules
}

# Get up to `limit` verified modules from the public registry for a namespace
# But pass the organization parameter
get_public_registry_modules = func() {
  req = http.request("https://" + address + "/v1/modules/"  +
                     organization + "?limit=" + limit + "&verified=true")

  # Call TFE API to get modules and unmarshal results
  res = json.unmarshal(http.get(req).body)

  # Initialize modules map
  modules = {}

  # Iterate over the modules and extract names, providers, and latest versions
  for res.modules as m {
    index = m.namespace + "/" + m.name + "/" + m.provider
    modules[index] = m.version
  }

  return modules
}

# Get all modules from the private registry by calling itself recursively
# Start by calling with modules = {}, and offset = 0
get_private_registry_modules = func(modules, offset) {
  req = http.request("https://" + address + "/api/registry/v1/modules/"  +
                    organization + "?limit=" + string(limit) +
                    "&offset=" + string(offset))
  req = req.with_header("Authorization", "Bearer " + token)

  # Call TFE API to get modules and unmarshal results
  res = json.unmarshal(http.get(req).body)

  # Iterate over the modules and extract names, providers, and latest versions
  for res.modules as m {
    index = m.namespace + "/" + m.name + "/" + m.provider
    modules[index] = m.version
  }

  # Make recursive function call if there are more modules
  if res.meta.next_offset else 0 > 0 {
    return get_private_registry_modules(modules, res.meta.next_offset)
  }

  return modules
}

# Validate sources of modules in the registry
validate_modules = func() {

  validated = true

  # Get latest module versions from registry
  discovered_modules = retrieve_latest_module_versions()

  # Get all module addresses
  allModuleAddresses = config.find_descendant_modules("")
  #print("Module addresses:", allModuleAddresses)

  # Iterate over all modules in the tfconfig/v2 import
  for allModuleAddresses as module_address {
    moduleCalls = config.find_module_calls_in_module(module_address)
    # Iterate over modules of the current module
    for moduleCalls as index, m {
      # Check if module is in the PMR
      if strings.has_prefix(m.source, address + "/" + organization) {
        # Check version constraint of module against latest version in PMR
        org_name_provider = strings.trim_prefix(m.source, address + "/")
        most_recent_version = discovered_modules[org_name_provider]
        if not version.new(most_recent_version).satisfies(m.version_constraint) {
          if length(module_address) == 0 {
            print("PMR module", m.source, "used in the root module",
                  "has version constraint", m.version_constraint, "that does not",
                  "allow the most recent version", most_recent_version)
            validated = false
          } else {
            print("PMR module", m.source, "used in module", module_address,
                  "has version constraint", m.version_constraint, "that does not",
                  "allow the most recent version", most_recent_version)
            validated = false
          } // end root module check
        } // end version check
      } else {
        # Check version constraint of module against latest version in public registry
        most_recent_version = discovered_modules[m.source]
        if m.source in keys(discovered_modules) {
          most_recent_version = discovered_modules[m.source]
          if not version.new(most_recent_version).satisfies(m.version_constraint) {
            if length(module_address) == 0 {
              print("Public registry module", m.source, "used in the root module",
                    "has version constraint", m.version_constraint, "that does not",
                    "allow the most recent version", most_recent_version)
              validated = false
            } else {
              print("Public registry module", m.source, "used in module",
                    module_address, "has version constraint", m.version_constraint,
                    "that does not allow the most recent version", most_recent_version)
              validated = false
            } // end root module check
          } // end version check
        } // end source in keys
      } // end if module in PMR or public registry
    } // end for module calls
  } // end modules

  return validated
}

##### Rules #####

# Call the validation function
modules_validated = validate_modules()

# Main rule
main = rule {
  modules_validated
}
